#
# Copyright 2018 Couchbase, Inc. All rights reserved.
#

CMAKE_MINIMUM_REQUIRED(VERSION 2.8.9)
PROJECT(libjsonsm)

SET(CMAKE_INSTALL_RPATH_USE_LINK_PATH TRUE)
LIST(FIND CMAKE_PLATFORM_IMPLICIT_LINK_DIRECTORIES "${CMAKE_INSTALL_PREFIX}/lib" isSystemDir)
IF("${isSystemDir}" STREQUAL "-1")
  SET(CMAKE_INSTALL_RPATH "${CMAKE_INSTALL_PREFIX}/lib")
ENDIF("${isSystemDir}" STREQUAL "-1")

SET(LJSM_GENSRCDIR ${PROJECT_BINARY_DIR}/generated)
INCLUDE_DIRECTORIES(${LJSM_GENSRCDIR})
SET(LJSM_GENINFODIR ${PROJECT_SOURCE_DIR}/cmake/distinfo)

SET(SOURCE_ROOT ${PROJECT_SOURCE_DIR})

INCLUDE(cmake/Modules/GetLibraryFlags.cmake)
INCLUDE(cmake/Modules/FindProfiler.cmake)
INCLUDE(cmake/Modules/GetPlatformCCInfo.cmake)
INCLUDE(cmake/Modules/GetVersionInfo.cmake)
INCLUDE(CheckIncludeFiles)
INCLUDE(cmake/source_files.cmake)

IF(LIB_INSTALL_DIR)
  SET(CMAKE_INSTALL_LIBDIR "${LIB_INSTALL_DIR}")
ENDIF()
IF(NOT LJSM_USE_ARCHLIBDIR AND NOT CMAKE_INSTALL_LIBDIR)
  SET(CMAKE_INSTALL_LIBDIR "lib")
ENDIF()
INCLUDE(GNUInstallDirs)


SET(CPACK_PACKAGE_VERSION_MAJOR "${LJSM_VERSION_MAJOR}")
SET(CPACK_PACKAGE_VERSION_MINOR "${LJSM_VERSION_MINOR}")
SET(CPACK_PACKAGE_VERSION_PATCH "${LJSM_VERSION_PATCH}")
IF(NOT CMAKE_BUILD_TYPE)
  SET(CPACK_INSTALL_SCRIPT
    "${PROJECT_SOURCE_DIR}/cmake/Modules/DistScript.cmake")
ENDIF()
SET(ljsonsm_package_name "libjsonsm-${LJSM_VERSION}_${LJSM_ARCH_STRING}_${LJSM_CC_STRING}")
SET(CPACK_PACKAGE_FILE_NAME ${ljsonsm_package_name})

IF(WIN32)
  SET(CPACK_GENERATOR "ZIP")
ELSE()
  SET(CPACK_GENERATOR "TXZ")
ENDIF()

INCLUDE(CPack)

CONFIGURE_FILE(
  ${PROJECT_SOURCE_DIR}/include/libjsonsm/configuration.h.in
  ${LJSM_GENSRCDIR}/libjsonsm/configuration.h
  @ONLY)

CONFIGURE_FILE(
  ${PROJECT_SOURCE_DIR}/cmake/defs.mk.in
  ${PROJECT_BINARY_DIR}/defs.mk)

SET(CMAKE_RUNTIME_OUTPUT_DIRECTORY ${PROJECT_BINARY_DIR}/bin)
SET(CMAKE_ARCHIVE_OUTPUT_DIRECTORY ${PROJECT_BINARY_DIR}/lib)
IF(WIN32)
  SET(CMAKE_LIBRARY_OUTPUT_DIRECTORY ${PROJECT_BINARY_DIR}/bin)
ELSE()
  SET(CMAKE_LIBRARY_OUTPUT_DIRECTORY ${PROJECT_BINARY_DIR}/lib)
ENDIF()

INCLUDE_DIRECTORIES(BEFORE ${SOURCE_ROOT}/include
  ${SOURCE_ROOT}/contrib
  ${SOURCE_ROOT}/src
  ${SOURCE_ROOT})

MACRO(LJSM_UTIL tgt)
  SET_TARGET_PROPERTIES(${tgt}
    PROPERTIES
    COMPILE_FLAGS "${LJSM_CORE_CFLAGS}"
    POSITION_INDEPENDENT_CODE TRUE)
ENDMACRO()

ADD_LIBRARY(jsonsmcore OBJECT ${LJSM_CORE_SRC})
SET_TARGET_PROPERTIES(jsonsmcore PROPERTIES
  COMPILE_FLAGS "${LJSM_CORE_CFLAGS}"
  POSITION_INDEPENDENT_CODE TRUE)

SET(LJSM_CORE_OBJS
  $<TARGET_OBJECTS:jsonsmcore>)

ADD_LIBRARY(jsonsm SHARED ${LJSM_CORE_OBJS} ${LIBPROFILE})

IF(NOT APPLE)
  SET_TARGET_PROPERTIES(jsonsm PROPERTIES
    SOVERSION "${LJSM_SONAME_MAJOR}"
    VERSION "${LJSM_SONAME_FULL}")
ENDIF()


SET_TARGET_PROPERTIES(jsonsm PROPERTIES PREFIX "lib")
SET_TARGET_PROPERTIES(jsonsm PROPERTIES IMPORT_PREFIX "lib")
SET(LJSM_LINK_DEPS "m")
IF(CMAKE_SYSTEM_NAME STREQUAL "Linux")
  SET(LJSM_LINK_DEPS ${LJSM_LINK_DEPS} rt)
ENDIF()

TARGET_LINK_LIBRARIES(jsonsm ${LJSM_LINK_DEPS})

ADD_SUBDIRECTORY(tools tools)
INSTALL(TARGETS jsonsm
  RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}
  LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
  ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR})
INSTALL(DIRECTORY include/libjsonsm ${LJSM_GENSRCDIR}/libjsonsm
  DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}
  FILES_MATCHING PATTERN *.h)

SET(_ljsonsm_tarname "libjsonsm-${LJSM_VERSION}")
SET(_ljsonsm_manifest "${LJSM_GENINFODIR}/MANIFEST")

ADD_CUSTOM_TARGET(uninstall
  COMMAND xargs rm -vf < ${CMAKE_CURRENT_BINARY_DIR}/install_manifest.txt)

ADD_CUSTOM_TARGET(file_manifest
  COMMAND sh -c 'test -e ${_ljsonsm_manifest} || git ls-files > ${_ljsonsm_manifest}'
  WORKING_DIRECTORY ${PROJECT_SOURCE_DIR})

ADD_CUSTOM_TARGET(dist
  COMMAND rm -rf "${_ljsonsm_tarname}"
  COMMAND mkdir "${_ljsonsm_tarname}"
  COMMAND tar -cf - -C ${PROJECT_SOURCE_DIR} -T ${_ljsonsm_manifest} | tar xf - -C "${_ljsonsm_tarname}"
  COMMAND cp -a "${LJSM_GENINFODIR}" "${_ljsonsm_tarname}/cmake/"
  COMMAND tar -czf "${_ljsonsm_tarname}.tar.gz" "${_ljsonsm_tarname}"
  COMMAND rm -rf "${_ljsonsm_tarname}"
  DEPENDS file_manifest)

INCLUDE(GenerateExportHeader)
GENERATE_EXPORT_HEADER(jsonsm
  EXPORT_MACRO_NAME LJSM_PUBLIC_API
  EXPORT_FILE_NAME ${PROJECT_SOURCE_DIR}/include/libjsonsm/visibility.h)

# Generate our configuration file _after_ we've collected everything
INCLUDE(cmake/Modules/GenerateConfigDotH.cmake)

# Build any local tests/scripts
IF (EXISTS ${SOURCE_ROOT}/tests/LOCAL)
  ADD_SUBDIRECTORY(${SOURCE_ROOT}/tests/LOCAL)
ENDIF()
